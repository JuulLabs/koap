plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jmailen.kotlinter'
}

kotlin {
    jvm {
        apply plugin: 'java' // Needed by JaCoCo when using `org.jetbrains.kotlin.multiplatform`
        apply plugin: 'jacoco'
        apply plugin: 'com.vanniktech.maven.publish'

        kotlinter {
            disabledRules = ["no-multi-spaces"]
        }

        jacoco {
            toolVersion = "0.8.5"
        }

        jacocoTestReport {
            reports {
                csv.enabled false
                html.enabled true
                xml.enabled true
            }

            classDirectories.setFrom(file("${buildDir}/classes/kotlin/jvm/"))
            sourceDirectories.setFrom(files("src/commonMain", "src/jvmMain"))
            executionData.setFrom(files("${buildDir}/jacoco/jvmTest.exec"))
        }

        compilations.main.kotlinOptions.jvmTarget = "1.8"
        compilations.test.kotlinOptions.jvmTarget = "1.8"
    }

    js {
        apply plugin: 'kotlinx-serialization'
        browser()
    }

    sourceSets {
        commonMain {
            dependencies {
                api deps.kotlin.stdlib
                implementation deps.okio
                implementation deps.serialization.common
            }
        }

        commonTest {
            dependencies {
                implementation deps.kotlin.test.annotations
                implementation deps.kotlin.test.common
            }
        }

        jvmTest {
            dependencies {
                implementation deps.kotlin.test.jvm
                implementation deps.mockk
                implementation deps.equalsverifier
            }
        }

        jsTest {
            dependencies {
                implementation deps.kotlin.test.js
            }
        }
    }
}
